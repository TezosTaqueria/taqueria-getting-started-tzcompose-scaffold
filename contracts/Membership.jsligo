namespace Membership {
    export type storage = map<address, bool>;

    @entry
    const join = (_: unit, store: storage): [list<operation>, storage] => {
        // Check if 1 tez has been provided
        const amount = Tezos.get_amount();
        if (amount < 1tez) {
            failwith("Insufficient amount of tez");
        }

        // Get the sender address
        const sender = Tezos.get_sender();

        // Add the sender to the storage
        const updatedStore: map<address, bool> = Map.add(sender, true, store);

        return [list([]), updatedStore];
    }

    @entry
    const leave = (_:unit, store: storage): [list<operation>, storage] => {
        // Get the sender address
        const sender = Tezos.get_sender();

        // Add the sender to the storage
        const updatedStore  = Map.remove(sender, store);

        return [list([]), updatedStore];
    }

    @view
    const is_member = (addr: address, store: storage) => {
        // Check if the sender is a member
        return Map.find(addr, store);
    }
}


const test_join = () : unit => {
    let result = Test.Next.Originate.contract(contract_of(Membership), Map.empty, 0tez);
    let contract = Test.Next.Typed_address.to_contract(result.taddr);
    const status = Test.Next.Contract.transfer(contract, Join(unit), 1tez);
    const fromAddr = Test.Next.Account.address(1);
    match(status) {
        when(Success(_)): do {
            Assert.assert(true);

            // Get expected and actual values for storage
            let expected: map<address, bool> = Map.empty;
            expected = Map.add(fromAddr, true, expected);
            const actual = Test.Next.Typed_address.get_storage(result.taddr);

            // Debug actual and expected values
            // Test.Next.IO.print(Test.Next.Michelson.to_json(expected));
            // Test.Next.IO.print("\n");
            // Test.Next.IO.print(Test.Next.Michelson.to_json(actual));
            // Test.Next.IO.print("\n");

            // Assert that expected storage is equal to the actual storage
            Assert.assert(Test.Next.Compare.eq(expected, actual));

            // Assert that is_member view works as expected
            const addr = Test.Next.Typed_address.to_address(result.taddr);
            const viewResult: option<bool> = Tezos.call_view("is_member", Tezos.get_sender(), addr);
            match(viewResult) {
                when(Some(_)): Assert.assert(true);
                when(_): Assert.assert(false);
            }
        }
        when(_): Assert.assert(false);
    }
}

const test_leave = () : unit => {
    let result = Test.Next.Originate.contract(contract_of(Membership), Map.empty, 0tez);
    let contract = Test.Next.Typed_address.to_contract(result.taddr);
    Test.Next.Contract.transfer(contract, Join(unit), 0tez);
    const status = Test.Next.Contract.transfer(contract, Leave(unit), 0tez);
    match(status) {
        when(Success(_)): do {
            Assert.assert(true);
            const expected: map<address, bool> = Map.empty;
            const actual = Test.Next.Typed_address.get_storage(result.taddr);
            Assert.assert(Test.Next.Compare.eq(expected, actual));
        }
        when(_): Assert.assert(false);
    }
}

const test_join_result = test_join()
const test_leave_result = test_leave()

